#!/usr/bin/perl

use v5.12;
use warnings;

use List::Util qw'max sum';

=head1 NAME

elftool

=head1 FUNCTIONS

=over

=item elf_stuff(@_)

Return array of arrays of the stuff carried by an elf.
Empty strings delimit elves.

=cut

sub elf_stuff {
    my @elves;
    my $elf = [];
    for my $stuff (@_) {
        if ($stuff eq '') {
            push(@elves, $elf);
            $elf = [];
            next;
        }
        push(@$elf, $stuff)
    }
    \@elves
}

=item stuff_sums($elves)

Return array of sums of all the stuff carried by each elf.
$elves is a reference to an array of arrays of elf stuff.

=cut

sub stuff_sums {
    local $_;
    my @sums = map { sum(@$_) } @{shift()};
    \@sums
}

=item mostest_of_the_elves($elves)

Return the total of all the stuff carried by the elf carrying the most stuff.
$elves is a reference to an array of arrays of elf stuff.

=cut

sub mostest_of_the_elves {
    max(@{stuff_sums(@_)})
}

=item top_three_sum_of_the_elves($elves)

Return the total of all the stuff carried by the top three elves carrying the most stuff.
$elves is a reference to an array of arrays of elf stuff.

=cut

sub top_three_sum_of_the_elves {
    sum((sort({ $b <=> $a } @{stuff_sums(@_)}))[0 .. 2])
}

=back

=cut

sub main {
    my $stuff = elf_stuff(map { chomp; $_ } <>);
    say('the elf carrying the most calorically great food carries ', mostest_of_the_elves($stuff));
    say('the top three elves carrying the most calorically caloric food carry in total amongst themselves ',
        top_three_sum_of_the_elves($stuff));
    0
}

if (!caller) {
    exit(main(@ARGV))
}

1;
